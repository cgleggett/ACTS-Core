cmake_minimum_required(VERSION 3.5)

project (ACTS VERSION 0.6.0 LANGUAGES CXX)

list (APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_SOURCE_DIR}/cmake)

# build options

# all options must be defined here to keep the list directly visible and
# make the option variables available everywhere
option (ACTS_BUILD_LEGACY "Build legacy package" ON)
option (ACTS_BUILD_DOC "Build documentation" OFF)
option (ACTS_BUILD_EXAMPLES "Build examples" OFF)
option (ACTS_BUILD_TESTS "Build unit tests" ON)
option (ACTS_BUILD_INTEGRATION_TESTS "Build integration tests" OFF)
option (ACTS_BUILD_DD4HEP_PLUGIN "Build DD4HEP plugins" OFF)
option (ACTS_BUILD_MATERIAL_PLUGIN "Build Material plugins" OFF)
option (ACTS_BUILD_TGEO_PLUGIN "Build TGeo plugins" OFF)
option (ACTS_BUILD_JSON_PLUGIN "Build Json plugin" OFF)
option (ACTS_BUILD_PROTOBUF_PLUGIN "Build Protobuf plugin" OFF)
# all other compile-time parameters must be defined here for clear visibility
# and to avoid forgotten options somewhere deep in the hierarchy
set (ACTS_PARAMETER_DEFINITIONS_PLUGIN "ACTS/Utilities/detail/DefaultParameterDefinitions.hpp" CACHE FILEPATH "Default track parameter definition")

# handle inter-plugin dependencies
if (ACTS_BUILD_DD4HEP_PLUGIN)
  set (ACTS_BUILD_TGEO_PLUGIN ON)
endif()

# required packages

find_package(Boost 1.62 REQUIRED COMPONENTS program_options unit_test_framework)
find_package(Eigen 3.2.9 REQUIRED)

# optional packages

if(ACTS_BUILD_DOC)
  find_package(Doxygen 1.8.11 REQUIRED)
endif()
if(ACTS_BUILD_TESTS)
  find_package(PythonInterp 2 EXACT REQUIRED)
endif()
if(ACTS_BUILD_DD4HEP_PLUGIN)
  find_package(DD4hep 1.2 REQUIRED COMPONENTS DDCore DDSegmentation)
endif()
if(ACTS_BUILD_TGEO_PLUGIN)
  find_package(ROOT 6.10 REQUIRED COMPONENTS Geom)
endif()

# additional configuration and tools

include (GNUInstallDirs) # GNU-like installation paths, e.g. lib/, include/, ...
include (ACTSCompilerOptions) # default compile options
include (ACTSFunctions) # ACTS-specific helper functions
include (ACTSComponentsHelpers) # handle components via add_..._if commands

# core library and plugins

add_component (Core)
add_component_if(Legacy ACTS_BUILD_LEGACY)
add_subdirectory (Plugins)

# examples and tests
if(ACTS_BUILD_TESTS OR ACTS_BUILD_INTEGRATION_TESTS)
  include(CTest) # must be included from the main CMakeLists.txt
endif()
add_subdirectory_if(Examples ACTS_BUILD_EXAMPLES)
add_subdirectory_if(Tests ACTS_BUILD_TESTS)
add_subdirectory_if(IntegrationTests ACTS_BUILD_INTEGRATION_TESTS AND ACTS_BUILD_LEGACY)

# documentation
add_subdirectory_if (doc ACTS_BUILD_DOC)

# setup script and cmake configuration files

configure_file (${CMAKE_CURRENT_SOURCE_DIR}/setup.sh.in ${CMAKE_CURRENT_BINARY_DIR}/setup.sh)
install (FILES ${CMAKE_CURRENT_BINARY_DIR}/setup.sh DESTINATION ${CMAKE_INSTALL_BINDIR})
include (ACTSCreateConfig)
